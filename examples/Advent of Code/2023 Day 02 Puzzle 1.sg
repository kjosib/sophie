# See https://adventofcode.com/2023/day/2 for an explanation.

# While this solution was under development,
# the type checker bugged out on the definition of "surely".
# I had to write a method for how to deal with
# type variables like "?x" in function signatures.

# Also, while working on this puzzle I moved the "filesystem"
# actor to the standard preamble.

# I managed to trigger the "absurd"-ity in "surely" as a
# consequence of writing a bug in the "split_one" function:
# In place of the expression "len(haystack)", I had
# accidentally written "len(needle)", making "suffix" empty.

import:

"2023 Day 02 common" (surely, split, split_one);

define:

solve(lines) = console ! echo[str(goal(lines)), EOL];
goal(lines) = sum(map(parse_game, lines));

parse_game(line) = game_id if possible_game(cut.snd) else 0 where
	cut = split_one(": ", line);
    game_id = surely(val(mid(cut.fst, 5, 10)));
end parse_game;

possible_game(game) = all(map(possible_round, split('; ', game)));
possible_round(round) = all(map(possible_cubes, split(', ', round)));
possible_cubes(segment) = nr_cubes <= limit_for_color where
	nr_cubes = surely(val(it.fst));
	color = left(it.snd, 1);
	it = split_one(' ', segment);
	limit_for_color = case
		when color == 'r' then 12;
		when color == 'g' then 13;
		when color == 'b' then 14;
		else absurd;
	esac;
end possible_cubes;

begin:
filesystem!read_lines("/playground/advent of code/2023-d2", !solve);

