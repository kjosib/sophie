# Version of the number-guessing game which exercises tne
# new asynchronous message-passing semantics.

define:

EOL = chr(10);
intro = ["I have chosen a random number from 1 to 100.", EOL, EOL];

game(r) = turn(1) where
    goal = int(r*100)+1;
    turn(score) = do
        console:echo ["What is your guess? "];
        console:read(guess);
    end where
        guess(g) = case val(g) as v of
            this -> consider(int(v.item));
            nope -> do
                console:echo ["I didn't grok that number.", EOL];
                turn(score);
            end;
        esac;
        consider(g:number) = case
            when g > goal then again('high', 'low');
            when g < goal then again('low', 'high');
            else win;
        esac;
        again(far, near) = do
            console:echo ['Too ', far, '. Try a ', near, 'er number.', EOL];
            turn(score+1);
        end;
        win = console:echo ["You win after ", str(score), " guesses!", EOL];
    end turn;
end game;

begin:
    console:echo(intro);
    console:random(game);

