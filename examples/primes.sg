# This one's a bit more meat on the bones.

define:

primes(max) -> list[number] = NIL IF max < 2 ELSE cons(2, odd_primes) where
	odd_primes = more_primes(0, cons(4, map(square, odd_primes)), 3);
	more_primes(bound, squares, candidate_prime) = CASE
		WHEN candidate_prime > max THEN NIL;
		WHEN candidate_prime > squares.head THEN more_primes(bound+1, squares.tail, candidate_prime);
		WHEN is_prime THEN cons(candidate_prime, successors);
		ELSE successors;
	ESAC where
		is_prime = NOT any (map(is_divisor, take(bound, odd_primes)));
		is_divisor(p) = candidate_prime MOD p == 0;
		successors = more_primes(bound, squares, candidate_prime+2);
	end more_primes;
end primes;

some_squares = map(square, [1,2,3]);  # You can use words before you define them,
square(x) = x * x;                    # as long as you do get around to defining them.

begin:
	"Here's a list of square numbers:";
	some_squares;
	"Here are the first few dozen prime numbers:";
	primes(256);
end.
